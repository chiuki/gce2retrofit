final DEVELOPMENT = true

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.bmuschko:gradle-nexus-plugin:2.2'
  }
}

apply plugin: 'groovy'
apply plugin: 'java'

if (!DEVELOPMENT) {
  apply plugin: 'com.bmuschko.nexus'
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'com.android.tools.build:gradle:1.2.3'

  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'com.squareup:javawriter:2.5.1'
  compile 'org.apache.commons:commons-lang3:3.3.2'
  compile 'commons-cli:commons-cli:1.2'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.25'
}

jar {
  doFirst {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  }
  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
  manifest.attributes("Main-Class": "com.sqisland.gce2retrofit.Generator")
  archiveName "gce2retrofit.jar"
}

apply plugin: 'maven'

group = 'com.sqisland'
version = '1.1.0-SNAPSHOT'

install {
  repositories {
    mavenInstaller {
      pom.artifactId = 'gce2retrofit'
    }
  }
}
uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://$buildDir/repo")
      pom.artifactId = 'gce2retrofit'
    }
  }
}

if (!DEVELOPMENT) {
  modifyPom {
    project {
      name 'gce2retrofit'
      description "Gradle plugin to generate Retrofit classes from Google Cloud Endpoint (GCE) discovery file"
      url 'https://github.com/chiuki/gce2retrofit'
      inceptionYear '2015'

      scm {
        url 'https://github.com/chiuki/gce2retrofit'
        connection 'scm:git:git://github.com/chiuki/gce2retrofit.git'
        developerConnection 'scm:git:ssh://git@github.com/chiuki/gce2retrofit.git'
      }

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'chiuki'
          name 'Chiu-Ki Chan'
          email 'chiuki@sqisland.com'
        }
      }
    }
  }
}
